# üöÄ ZINIT PLUGIN MANAGER SETUP
if [[ ! -f $HOME/.local/share/zinit/zinit.git/zinit.zsh ]]; then
    print -P "%F{33} %F{220}Installing %F{33}ZDHARMA-CONTINUUM%F{220} Initiative Plugin Manager (%F{33}zdharma-continuum/zinit%F{220})‚Ä¶%f"
    command mkdir -p "$HOME/.local/share/zinit" && command chmod g-rwX "$HOME/.local/share/zinit"
    command git clone https://github.com/zdharma-continuum/zinit "$HOME/.local/share/zinit/zinit.git" && \
        print -P "%F{33} %F{34}Installation successful.%f%b" || \
        print -P "%F{160} The clone has failed.%f%b"
fi

source "$HOME/.local/share/zinit/zinit.git/zinit.zsh"
autoload -Uz _zinit
(( ${+_comps} )) && _comps[zinit]=_zinit

zinit light-mode for \
    zdharma-continuum/zinit-annex-as-monitor \
    zdharma-continuum/zinit-annex-bin-gem-node \
    zdharma-continuum/zinit-annex-patch-dl \
    zdharma-continuum/zinit-annex-rust

# üåç ENVIRONMENT VARIABLES
export EDITOR='nvim'
export COLORTERM='truecolor'
export HOMEBREW_NO_ENV_HINTS=1
export LANG=zh_CN.UTF-8
export LANGUAGE=zh_CN:en_US
export LC_ALL=zh_CN.UTF-8
export LC_MESSAGES=zh_CN.UTF-8
export BAT_THEME=tokyonight_day
export GIT_EDITOR='nvim'

# Initialize Homebrew (adds /opt/homebrew/bin and /opt/homebrew/sbin to PATH)
if [[ -x /opt/homebrew/bin/brew ]]; then
  eval "$(/opt/homebrew/bin/brew shellenv)"
  fpath=($(/opt/homebrew/bin/brew --prefix)/share/zsh/site-functions $fpath)
fi

# Add custom paths (after Homebrew to ensure proper order)
path=("$HOME/.cargo/bin" "$HOME/.bun/bin" $path)
export PATH

# üìö HISTORY CONFIGURATION
HISTSIZE=10000
HISTFILE=~/.zsh_history
SAVEHIST=$HISTSIZE
HISTDUP=erase

setopt appendhistory sharehistory hist_ignore_space hist_ignore_all_dups \
       hist_save_no_dups hist_ignore_dups hist_find_no_dups auto_list

# üîß COMPLETION SYSTEM SETUP
ZSH_CACHE_DIR="${ZSH_CACHE_DIR:-$HOME/.cache/zsh}"
[[ -d "$ZSH_CACHE_DIR" ]] || mkdir -p "$ZSH_CACHE_DIR"

autoload -Uz compinit
compinit -u -d "$ZSH_CACHE_DIR/.zcompdump"

if [[ -n "${HOMEBREW_PREFIX:-}" ]] && [[ -f "${HOMEBREW_PREFIX}/share/zsh/site-functions/_mise" ]]; then
  (( ! $+functions[_mise] )) && { autoload -Uz _mise; compdef _mise mise; }
fi

zstyle ':completion:*' use-cache on
zstyle ':completion:*' cache-path "$ZSH_CACHE_DIR/.zcompcache"
zstyle ':completion:*' matcher-list 'm:{a-z}={A-Za-z}'
zstyle ':completion:*' list-colors "${(s.:.)LS_COLORS}"
zstyle ':completion:*' menu no

# üîß ZSH CORE FEATURES & PLUGINS
zinit wait"1" lucid for \
    OMZL::clipboard.zsh \
    OMZL::history.zsh \
    OMZL::completion.zsh

zinit for lucid light-mode blockf \
    zdharma-continuum/fast-syntax-highlighting \
    zsh-users/zsh-autosuggestions \
    zsh-users/zsh-completions

zinit ice lucid wait'0'
zinit light Aloxaf/fzf-tab

zinit ice depth=1
zinit light jeffreytse/zsh-vi-mode

zinit wait"2" lucid for \
    OMZP::git \
    OMZP::archlinux \
    OMZP::rust \
    OMZP::eza \
    OMZP::bun

# üîç FZF CONFIGURATION
export FZF_DEFAULT_OPTS="$FZF_DEFAULT_OPTS \
    --color=fg:-1,fg+:-1,bg:-1,bg+:20,pointer:-1,header:1,border:4,hl:16,hl+:17,info:21,prompt:2,marker:21,spinner:21,scrollbar:dim"
export FZF_CTRL_T_COMMAND="$FZF_DEFAULT_COMMAND"
export FZF_ALT_COMMAND="fd --type=d --hidden --strip-cwd-prefix --exclude /git"
export FZF_CTRL_T_OPTS="--preview 'bat -n --color=always --line-range :500 {}'"
export FZF_ALT_C_OPTS="--preview 'eza --tree --color=always {} | head -200'"

_fzf_compgen_path() { fd --hidden --exclude .git . "$1" }
_fzf_compgen_dir() { fd --type=d --hidden --exclude .git . "$1" }

# ‚öôÔ∏è PLUGIN CONFIGURATION
zstyle ':fzf-tab:complete:cd:*' fzf-preview 'ls --color $realpath'
zstyle ':fzf-tab:complete:cd:*' popup-pad 30 0
zstyle ':fzf-tab:complete:__zoxide_z:*' fzf-preview 'ls --color $realpath'
zstyle ':fzf-tab:*' fzf-command ftb-tmux-popup

ZSH_AUTOSUGGEST_STRATEGY=(history completion)

# üîó ALIASES
alias cls='clear'
alias ..='cd ..'
alias ls='eza --icons --group-directories-first --color=always --time-style=long-iso'
alias ll='ls -lh'
alias la='ll -a'
alias lla='ls -a'
alias lg='lazygit'

# üìù CUSTOM FUNCTIONS
yy() {
    local tmp="$(mktemp -t "yazi-cwd.XXXXXX")" cwd
    yazi "$@" --cwd-file="$tmp"
    cwd="$(< "$tmp")" && [[ -n "$cwd" && "$cwd" != "$PWD" ]] && cd -- "$cwd"
    rm -f -- "$tmp"
}

# üöÄ TOOL INITIALIZATION
export ATUIN_NOBIND=true
eval "$(atuin init zsh)"
bindkey '^r' atuin-search

eval "$(mise activate zsh)"

[[ -f /opt/homebrew/share/zsh/site-functions/_npm ]] && \
  source /opt/homebrew/share/zsh/site-functions/_npm

eval "$(zoxide init --cmd cd zsh)"
eval "$(fzf --zsh)"
eval "$(starship init zsh)"

# üéÜ STARTUP
fastfetch --logo none --config examples/13
